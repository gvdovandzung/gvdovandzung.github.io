/*
  icc

*!  VERSION 1.0.0  18may2012

  keyword:  rclass

*/

VERSION 12.0

INCLUDE _std_large
DEFINE _dlght 285
INCLUDE header

HELP hlp1, view("help icc")
RESET res1

SCRIPT PREINIT
BEGIN
	create DOUBLE val
END

DIALOG main, label("icc - Intraclass correlation coefficients")		///
	tabtitle("Main")
BEGIN
  GROUPBOX gb_type		_lft	_top	_iwd	_ht5h,		///
	label("Analysis-of-variance model")
  RADIO rb_onewayrandom		_ilft	_ss	_inwd	., first	///
	onclickon(script main_onewayrandom_on)				///
	label("One-way random effects")
  RADIO rb_twowayrandom		@	_ss	@	.,		///
	onclickon(script main_twowayrandom_on)				///
	label("Two-way random effects")
  RADIO rb_twowaymix		@	_ss	@	., last		///
	onclickon(script main_twowaymix_on)				///
	label("Two-way mixed effects")
  TEXT tx_dvar			_lft	_ls	_vnwd	.,		///
	label("Dependent variable:")
  DEFINE holdy @y
  VARNAME vn_dvarlist		@	_ss	@	.,		///
	label("Dependent variable")
  TEXT tx_targetvar		_lft3_2	holdy	@	.,		///
	label("Target variable:")
  VARNAME vn_targetvarlist	@	_ss	@	.,		///
	label("Target variable")
  TEXT tx_ratervar		_lft3_3	holdy	@	.,		///
	label("Rater variable:")
  VARNAME vn_ratervarlist	@	_ss	@	.,		///
	label("Rater variable")
  GROUPBOX gb_opts		_lft	_ms	_iwd	_ht5,		///
	label("Options")
  GROUPBOX gb_agreement		_ilft	_ms	_inwd	_ht3h,		///
	label("Choose agreement measure")

  RADIO rb_rand_absolute	_indent	_ss	@	., first	///
	option(absolute)						///
	label("Absolute agreement (Correlation)")
  DEFINE holdy @y
  RADIO rb_rand_consistency	@	_ss	@	., last		///
	option(consistency)						///
	label("Consistency of agreement")

  RADIO rb_mixed_consistency	@	holdy	_inwd	., first	///
	option(consistency)						///
	label("Consistency of agreement (Correlation)")
  RADIO rb_mixed_absolute	@	_ss	@	., last		///
	option(absolute)						///
	label("Absolute agreement")
  TEXT tx_testvalue		_ilft	_xls	155	.,		///
	label("Test intraclass correlation = ")
  EDIT ed_testvalue		165	@	_en7wd	.,		///
	option(testvalue) numonly default(0)				///
	label("Test intraclass correlation = ")
END

SCRIPT main_onewayrandom_on
BEGIN
	main.tx_ratervar.hide
	main.vn_ratervarlist.hide
	
	main.gb_agreement.hide
	main.rb_rand_absolute.hide
	main.rb_rand_consistency.hide
	main.rb_mixed_consistency.hide
	main.rb_mixed_absolute.hide

	main.gb_opts.setposition . 155 . 50
	main.ed_testvalue.setposition . 175
	main.tx_testvalue.setposition . 175
END

SCRIPT main_twowayrandom_on
BEGIN
	main.tx_ratervar.show
	main.vn_ratervarlist.show
	
	main.gb_agreement.show
	main.rb_rand_absolute.show
	main.rb_rand_consistency.show
	main.rb_mixed_consistency.hide
	main.rb_mixed_absolute.hide

	main.gb_opts.setposition . 155 . 120
	main.tx_testvalue.setposition . 245
	main.ed_testvalue.setposition . 245
END

SCRIPT main_twowaymix_on
BEGIN
	main.tx_ratervar.show
	main.vn_ratervarlist.show
	
	main.gb_agreement.show
	main.rb_rand_absolute.hide
	main.rb_rand_consistency.hide
	main.rb_mixed_consistency.show
	main.rb_mixed_absolute.show

	main.gb_opts.setposition . 155 . 120
	main.tx_testvalue.setposition . 245
	main.ed_testvalue.setposition . 245
END

INCLUDE byifin

DIALOG rpt, tabtitle("Reporting")
BEGIN
  DEFINE   _x  _lft
  DEFINE   _cx _spr
  DEFINE   _y  _top
  INCLUDE  _sp_level

  CHECKBOX ck_format		_lft	_ls	_iwd	.,		///
	onclickon(script rpt_format_on)					///
	onclickoff(script rpt_format_off)				///
	label("Display format for statistics")
  EDIT en_format		+20	_ss	200	.,		///
	option(format)							///
	label("Display format for statistics")
  BUTTON bu_format		+205	@	80	.,		///
	label("Create...")						///
	onpush(script rpt_showFormatDialog)				///
	tooltip("Create display format")
END


SCRIPT rpt_showFormatDialog
BEGIN
	create STRING formatString
	create CHILD format_chooser
	format_chooser.setExitString formatString
	format_chooser.setExitAction "rpt.en_format.setvalue class formatString.value"
END

SCRIPT rpt_format_on
BEGIN
	rpt.en_format.enable
	rpt.bu_format.enable
END

SCRIPT rpt_format_off
BEGIN
	rpt.en_format.disable
	rpt.bu_format.disable
END

PROGRAM command
BEGIN
	put " " /program by_output
	put "icc "
	require main.vn_dvarlist
	varlist [main.vn_dvarlist]
	require main.vn_targetvarlist
	varlist [main.vn_targetvarlist]
	require main.vn_ratervarlist
	varlist [main.vn_ratervarlist]
	put " " /program ifin_output
	beginoptions
		if main.rb_twowaymix {
			put "mixed "
		}

		call val.setvalue class main.ed_testvalue.value
		if (val.islt(0) | val.isge(1)) {
			stopbox stop `"Invalid null hypothesis test value.  Number must be >= 0 and < 1."'
		}

		option radio(main rb_mixed_consistency rb_mixed_absolute)
		option radio(main rb_rand_consistency rb_rand_absolute)

		optionarg /hidedefault main.ed_testvalue
		optionarg /hidedefault rpt.sp_level
		if rpt.ck_format {
			require rpt.en_format
			optionarg rpt.en_format
		}
	endoptions
END
